many System
many System.IO
many System.Threading.Tasks

much Program
	very Basic so void much static
		such x 1;
		x so 2;
		such y "test";
		wow x;
		wow y;

	very FunctionCalls so void much static
		such x plz Math.Pow many 2 2;
		wow x;
		wow plz x.GetType;
		such y plz Convert.ToInt32 many x;
		wow so op y + 1;

	very Constructors so void much static
		such rand gimme Random;
		such z so op plz rand.Next + plz rand.Next + 1;
		wow z;

		such instance gimme MyType many 1 2;
		such result plz instance.Method;
		wow instance.data;
		wow result;

	very Overflow so void much static
		such max int.MaxValue;
		such overflow so op max + 1;
		wow overflow;

	very Input so void much static
		such input plz Console.ReadLine;
		wow input;
		plz Console.ReadLine;

	very AsyncFunction so Task much static much async
		to the moon plz Task.Delay many 1000;
		wow "many delay";

	very WithResource so void much static
		very resource such writer gimme StringWriter
		{
			plz writer.Write many "wow";
			wow plz writer.ToString;
		}

	such lockObject so object much readonly much static

	very WithLock so void much static
		many lock lockObject
		{
			such x 1;
		}

	very Conditional so void much static
		such x 0;

		such rand gimme Random;
		such variable plz rand.Next;
		
		rly so op variable <= 10
		{
			x so 1;
		}
		but rly so op variable <= 1000
		{
			x so 2;
		}
		but
		{
			x so 3;
		}

	very Main so void much static
		lockObject so gimme object;
		plz Basic;
		plz FunctionCalls;
		plz Constructors;
		plz Overflow;
		plz Input;
		plz WithResource;
		plz Conditional;
		such task plz AsyncFunction;
		plz task.Wait;